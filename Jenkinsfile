pipeline {
  agent any

  parameters {
    choice(
      name: 'ENVIRONMENT',
      choices: ['develop', 'qa', 'release.s.2025.08', 'release.s.2025.09', 'release.s.2025.10'],
      description: 'Ambiente a usar (solo informativo)'
    )
    string(name: 'BRANCH', defaultValue: 'develop', description: 'Rama a construir')
    string(name: 'IMAGE_TAG', defaultValue: '', description: 'Tag de la imagen Docker')
    string(name: 'DOCKER_REGISTRY_HOST', defaultValue: '', description: 'Registry para push (vacío = no push)')
  }

  environment {
    MVN_CMD = "mvn -B -U"

    IMAGE_NAME = "student-service"
    IMAGE_TAG = "${IMAGE_TAG ?: BUILD_NUMBER}"

    FULL_IMAGE = "${DOCKER_REGISTRY_HOST ? DOCKER_REGISTRY_HOST + '/' : ''}${IMAGE_NAME}:${IMAGE_TAG}"

    CRED_DB_URL  = "db-url-student"
    CRED_DB_USER = "db-user-student"
    CRED_DB_PASS = "db-pass-student"
    CRED_DB_PORT = "db-port-student"

    DOCKER_REGISTRY_CRED = "docker-registry-creds"

    ENV_DEPLOY_FILE = ".env.deploy"
  }

  options {
    timestamps()
    ansiColor('xterm')
  }

  stages {

    stage('Checkout') {
      steps {
        checkout([
          $class: 'GitSCM',
          branches: [[name: "*/${BRANCH}"]],
          userRemoteConfigs: [[
            url: 'https://github.com/IAndresPH/student-service.git',
            credentialsId: 'github-https'
          ]]
        ])
      }
    }

    stage('Generate .env.deploy') {
      steps {
        withCredentials([
          string(credentialsId: CRED_DB_URL, variable: 'DB_URL'),
          string(credentialsId: CRED_DB_USER, variable: 'DB_USERNAME'),
          string(credentialsId: CRED_DB_PASS, variable: 'DB_PASSWORD'),
          string(credentialsId: CRED_DB_PORT, variable: 'DB_PORT')
        ]) {
          sh """
            echo "# Generated by Jenkins - DO NOT COMMIT" > ${ENV_DEPLOY_FILE}
            echo "SERVER_PORT=\${DB_PORT}" >> ${ENV_DEPLOY_FILE}
            echo "DB_PORT=\${DB_PORT}" >> ${ENV_DEPLOY_FILE}
            echo "DB_URL=\${DB_URL}" >> ${ENV_DEPLOY_FILE}
            echo "DB_USERNAME=\${DB_USERNAME}" >> ${ENV_DEPLOY_FILE}
            echo "DB_PASSWORD=\${DB_PASSWORD}" >> ${ENV_DEPLOY_FILE}
          """
          sh "echo '.env.deploy generado correctamente (valores ocultos)'"
        }
      }
    }

    stage('Maven Package') {
      steps {
        sh "${MVN_CMD} -DskipTests -DskipITs clean package"
        archiveArtifacts artifacts: 'target/*.jar', fingerprint: true
      }
    }

    stage('Docker Build') {
      steps {
        sh "docker build --pull -t ${FULL_IMAGE} ."
      }
    }

    stage('Docker Push') {
      when {
        expression { return DOCKER_REGISTRY_HOST?.trim() }
      }
      steps {
        withCredentials([usernamePassword(
          credentialsId: DOCKER_REGISTRY_CRED,
          usernameVariable: 'DOCKER_USER',
          passwordVariable: 'DOCKER_PASS'
        )]) {
          sh """
            echo \$DOCKER_PASS | docker login ${DOCKER_REGISTRY_HOST} --username \$DOCKER_USER --password-stdin
            docker push ${FULL_IMAGE}
            docker logout ${DOCKER_REGISTRY_HOST}
          """
        }
      }
    }

  }

  post {
    success {
      echo "Pipeline completo. Imagen generada: ${FULL_IMAGE}"
    }
    failure {
      echo "Pipeline falló."
    }
    always {
      cleanWs()
    }
  }
}